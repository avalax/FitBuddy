buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.1'
    }
}

plugins {
    id "org.standardout.versioneye" version "1.4.0"
	id "com.github.ben-manes.versions" version "0.13.0"
	id "com.github.kt3k.coveralls" version "2.6.3"
}

allprojects {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.android.application'

android {
    packagingOptions {
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    compileSdkVersion 24
    buildToolsVersion '24.0.2'
    defaultConfig {
        applicationId "de.avalax.fitbuddy"
        minSdkVersion 19
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
	lintOptions {
		abortOnError false
    }
	testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
    if (System.getenv("KEYSTORE") && new File(System.getenv("KEYSTORE")).exists()) {
        signingConfigs {
            release {
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("KEYSTORE_PASSWORD")
                keyAlias System.getenv("KEY_ALIAS")
                keyPassword System.getenv("KEY_PASSWORD")
            }
        }
    } else {
        println 'KEYSTORE not set -> release build will not signed!'
    }
    buildTypes {
        debug {
            debuggable true
            testCoverageEnabled true
        }
        if (System.getenv("KEYSTORE") && new File(System.getenv("KEYSTORE")).exists()) {
            release {
                debuggable false
                minifyEnabled true
                zipAlignEnabled true
                proguardFiles "proguard.cfg"
                signingConfig signingConfigs.release
            }
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'de.bechte.junit:junit-hierarchicalcontextrunner:4.12.1'
    testCompile('org.robolectric:robolectric:3.1.2') {
        exclude module: 'commons-logging'
        exclude module: 'httpclient'
    }
    androidTestCompile 'com.android.support:support-annotations:24.2.1'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.5'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.android.support:support-v4:24.2.1'
    compile 'com.squareup.dagger:dagger:1.2.5'
    compile 'com.google.zxing:android-integration:3.3.0'
}

versioneye {
    exclude 'androidJacocoAgent', 'androidJacocoAnt'
}

apply plugin: 'jacoco'

jacoco {
    toolVersion = "0.7.7.201606060606"
}

task jacocoTestReport(type: JacocoReport) {
	dependsOn project.tasks.withType(Test)
    group = "Reporting"
    description = "Generate Jacoco coverage reports"

    classDirectories = fileTree(
            dir: "${project.buildDir}/intermediates/classes/debug",
            excludes: ['**/R.class',
                       '**/R$*.class',
                       '**/*$ViewInjector*.*',
                       '**/*$ViewBinder*.*',
                       '**/BuildConfig.*',
                       '**/Manifest*.*',
                       '**/*InjectAdapter*.class',
                       '**/*$ModuleAdapter*.class',
                       '**/*Application*.class',
                       '**/*Module.class',
                       '**/*Fragment*.class',
                       '**/*Activity*.class',
                       '**/*View.class',
                       '**/*Listener*.class']
    )

    sourceDirectories = files('src/main/java')
    executionData = files("${project.buildDir}/jacoco/testDebugUnitTest.exec")

    reports {
        xml.enabled = true
        html.enabled = true
        xml.destination = "${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        html.destination = "${project.buildDir}/reports/jacoco/test"
    }

    doFirst {
        new File("${project.buildDir}/intermediates/classes").eachFileRecurse { file ->
            if (file.name.contains('$$')) {
                file.renameTo(file.path.replace('$$', '$'))
            }
        }
    }
}

apply plugin: 'checkstyle'

checkstyle {
    toolVersion = "7.1.2"
}

task checkstyle(type: Checkstyle) {
	configFile rootProject.file('checkstyle.xml')
	source 'src/main/java'
	ignoreFailures true
	showViolations true
	include '**/*.java'
		
	classpath = files()
}

afterEvaluate {
	if (project.tasks.findByName('check')) {
		check.dependsOn('checkstyle')
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.14.1'
}

apply plugin: 'pmd'

pmd{
    toolVersion="5.5.1"
}

task pmd(type: Pmd, group: 'reporting', description: 'Inspect sourcecode for bugs') {
    ruleSetFiles = files("pmd.xml")
    ignoreFailures = true
    ruleSets = []
    source 'src'
    include '**/*.java'
    exclude '**/gen/**'
    exclude '**/test/**'

    reports {
        xml.enabled = true
        html.enabled = true
        xml {
            destination "$reportsDir/pmd/pmd.xml"
        }
        html {
            destination "$reportsDir/pmd/pmd.html"
        }
    }
}

afterEvaluate {
    if (project.tasks.findByName('check')) {
        check.dependsOn('pmd')
    }
}

apply plugin: 'findbugs'

findbugs{
    toolVersion="3.0.1"
}

task findbugs(type: FindBugs) {
    ignoreFailures = true
    classes = files('build/intermediates/classes/debug')
    source = fileTree('src/main/java/')
    classpath = files()
    excludeFilter = new File("findbugs.xml")

    effort = 'max'
    reports {
        xml.enabled = true
        html.enabled = false
        xml {
            destination "$reportsDir/findbugs/findbugs.xml"
        }
    }
}

afterEvaluate {
    if (project.tasks.findByName("check")) {
        check.dependsOn('findbugs')
    }
}
